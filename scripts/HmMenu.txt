// try Zform!
// (JOELwindows7)
// You know what, learn by copying examples. then we can discuss all the next later.
// https://github.com/Doom2fan/ZFormsExamples/blob/70200615945ba730a68aff98950929191b368cda/zscript/ZFormsExamples/SimpleMenu.ZS

class HmMenuHandler : LMBH_ZF_Handler
{
	// The menu this command handler belongs to.
    // We need this to be able to do anything with our menu.
	HmMenu link;
	
	override void buttonClickCommand(LMBH_ZF_Button caller, Name command)
    {
        // If the command is "aButton"...
        if (command == "aButton")
        {
            // Change the text's colour to a random colour.
            link.aLabel.SetTextColor (random [textColor] (Font.CR_BRICK, Font.CR_TEAL));
            //link.aLabel.textColor = random [textColor] (Font.CR_BRICK, Font.CR_TEAL);
        }
    }
}

class HmMenu: LMBH_ZF_GenericMenu 
{
	 // The menu's command handler.
    // We need a command handler so we can make our menu interactable.
    HmMenuHandler handler;

    // A font to use for text.
    Font smallFont;

    // A background image.
    LMBH_ZF_Image background;
    // A simple single-texture button.
    LMBH_ZF_Button aButton;
    // A text label.
    LMBH_ZF_Label aLabel;

    override void Init (Menu parent)
    {
        Vector2 baseRes = (320, 200);

        // Call GenericMenu's 'Init' function to do some required initialization.
        Super.Init (parent);
        // Set our base resolution to 320x200.
        SetBaseResolution (baseRes);

        // Get GZDoom's new options menu smallfont.
        smallFont = OptionFont ();

        // Create an instance of the handler.
        handler = new ('HmMenuHandler');
        // Set the handler's "link" pointer to us.
        handler.link = self;

        // Add a background.
        background = LMBH_ZF_Image.Create
        (
            // Position
            (0, 0),
            // Size
            (320, 200),
            // Image path/name
            "graphics/ZFormsExamples/Panel.png",
            // Alignment options
            LMBH_ZF_Image.AlignType_TopLeft
        );
        // Add the image element into the main frame.
        background.Pack (mainFrame);

        // Create the box image's textures.
        let boxTexture = LMBH_ZF_BoxTextures.CreateTexturePixels
        (
            // The texture itself.
            "graphics/ZFormsExamples/BoxTexture.png",
            // The top-left corner of the middle of the box.
            (32, 32),
            // The bottom-right corner of the middle of the box.
            (64, 64),
            // Whether to scale (true) or tile (false) the sides.
            false,
            // Whether to scale (true) or tile (false) the middle.
            false
        );
        // Add a box image.
        let boxSize = (128, 128);
        let aBoxImage = LMBH_ZF_BoxImage.Create
        (
            // Position
            ((baseRes.X - boxSize.X) / 2., (baseRes.Y - boxSize.Y) / 2.),
            // Size
            boxSize,
            // Texture
            boxTexture,
            // Scale
            (0.25, 0.25)
        );
        // Add the box image element into the main frame.
        aBoxImage.Pack (mainFrame);

        // Create the button's textures.
        let buttonIdle = LMBH_ZF_BoxTextures.CreateSingleTexture ("graphics/ZFormsExamples/SmallButtonIdle.png", true);
        let buttonHover = LMBH_ZF_BoxTextures.CreateSingleTexture ("graphics/ZFormsExamples/SmallButtonHovered.png", false);
        let buttonClick = LMBH_ZF_BoxTextures.CreateSingleTexture ("graphics/ZFormsExamples/SmallButtonClicked.png", false);
        // Add a button.
        aButton = LMBH_ZF_Button.Create
        (
            // Position
            ((baseRes.X - 18.) / 2., (baseRes.Y - 18.) / 2.),
            // Size
            (18, 18),
            // Our command handler
            cmdHandler: handler,
            // A command string for the button
            command: "aButton",
            // The button's textures
            inactive: buttonIdle,
            hover: buttonHover,
            click: buttonClick
        );
        // Add the button element into the main frame.
        aButton.Pack (mainFrame);

        // Add a label.
        aLabel = LMBH_ZF_Label.Create
        (
            // Position
            (0, aButton.GetPosY () + aButton.GetHeight () + 4),
            //(0, aButton.box.pos.Y + aButton.box.size.Y + 4),
            // Size.
            (0, smallFont.GetHeight ()),
            // The label's text
            text: "Click me! aaaaaaaaaaa",
            // The font to use
            fnt: smallFont,
            // Whether to automatically wrap the text or not
            wrap: false,
            // Whether to automatically resize the element based on the text width
            autoSize: true,
            // The text's colour
            textColor: Font.CR_WHITE
        );
        // Calculate the horizontal position for the label so that it's centered on the screen.
        aLabel.SetPosX ((baseRes.x - smallFont.stringWidth (aLabel.GetText ())) / 2.);
        //aLabel.setBox(((baseRes.x - smallFont.stringWidth (aLabel.text)) / 2.,aLabel.box.pos.y),alabel.box.size);
        // Add the label element to the main frame.
        aLabel.Pack (mainFrame);
    }
}