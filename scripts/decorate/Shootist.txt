// Shootist from Tyle-J, with URROVA fix
// https://www.doomworld.com/forum/topic/129542-modern-shootist-weapon-template/

// Template Weapon guy
Actor TylerConfigPlayer : Doomplayer
{
	Player.DisplayName "Tyler Config"
	
	Player.startitem "TemplateGUN"
	Player.startitem "clipcolt" 120
	//Player.startitem "ColtFillAmmo" 9
	Player.startitem "TemplateAK"
	Player.startitem "AKFillammo" 120
	Player.startitem "ClipAk" 200
	Player.startitem "RocketzLauncher"
	Player.startitem "TemplateRocketz" 120
	Player.StartItem "Fist"
	player.attackzoffset 5
}  

/*
------------------ WEAPONS --------------------
*/

// Colt1911
ACTOR TemplateGUN : weapon 29644
{
	//$Category Weapons
	//$Title Template Gun
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 0
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "ClipColt"
	//Weapon.AmmoType1 "Clip"
	Weapon.AmmoType2 "ColtFillAmmo"
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 3800
	
	Obituary "$COLTDEATH"
	
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage ""
	
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
	+WEAPON.WIMPY_WEAPON
    +FORCEXYBILLBOARD
	
	
	States
	{
		Ready:
			TUTR A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
			
		Deselect:
			TUTR A 1 A_Lower
			Wait
			
		Select:
			TUTR A 1 A_Raise
			Wait
		
		Fire:
			TUTR A 0 A_JumpIfInventory("ColtFillAmmo",1,1)
			Goto NoAmmo
			TUTR A 0 A_AlertMonsters
			TUTR A 0 A_ZoomFactor(0.98)
			TUTR A 1 BRIGHT A_FireBullets (1, 1, -1, 14, "Bchip", FBF_NORANDOM)
			TUTR A 0 A_FireCustomMissile("ShellSpawn",7,0,5,-1)
			//TUTR A 0 A_FireCustomMissile("PistolShellSpawn",7,0,5,-1)
			//TUTR A 0 A_FireCustomMissile("AKrifleCaseSpawn",7,0,5,-1)
			TUTR A 0 A_PlaySoundEx("CLTFIRE","SoundSlot7")
			TUTR A 0 A_ZoomFactor(1.0)
			TUTR A 0 A_Takeinventory("ColtFillAmmo",1)
			TUTR B 5 A_GunFlash
			TUTR A 5 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
			TUTR A 1 A_Refire
			Goto Ready		
			
		NoAmmo:
			TUTR A 0 A_PlaySound("CLTMPTY", 4)
			TUTR A 5 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOFIRE)
			TUTR A 0 A_JumpIfInventory("ClipColt",1,"Reload")
			Goto Ready
		
		Reload:
			TUTR A 1 A_WeaponReady
			TUTR A 0 A_JumpIfInventory("ColtFillAmmo",9,"Ready")
			TUTR A 0 A_JumpIfInventory("clipcolt",1,1)
			Goto NoAmmo
			TUTR A 0 A_ZoomFactor(1.0)
			TNT1 A 0 A_PlaySoundEx("CLTRLOAD","SOUNDSLOT6")
			TUTR EGHIGGGGG 1
			TUTR IJKGE 4
				
		InsertBullets:
			TUTR A 0 A_JumpIfInventory("ColtFillAmmo",9,4)
			TUTR A 0 A_JumpIfInventory("clipcolt",1,1)
			Goto Ready
			TUTR A 0 A_Giveinventory("ColtFillAmmo",1)
			TUTR A 0 A_Takeinventory("clipcolt",1)
			Goto InsertBullets
			TUTR A 0 A_Refire
			Goto Ready
			
		Flash:
			TNT1 A 2 A_Light2
			TUTR F 2 A_Light1
			TNT1 A 0 A_Light0
			Stop
		
		Spawn:
			CLT2 A -1
			Stop
			
	}
}

// AK-47
ACTOR TemplateAK : weapon 28614
{
	//$Category Weapons
	//$Title Template AK-47
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "ClipAK"
	Weapon.AmmoType2 "AKFillAmmo"
	Weapon.SlotNumber 4
	weapon.selectionorder 4800
	Obituary "$AKDEATH"
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "$GOT AK-47 "
	+WEAPON.NOAUTOFIRE
    +WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
	+WEAPON.WIMPY_WEAPON
    +FORCEXYBILLBOARD
	States
	{
		Ready:
			AK47 A 0 
		
		Ready2:
			AK47 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
			
		Deselect:
			AK47 A 1  A_Lower(8)
			Wait
			
		Select:
			AK47 A 1 A_Raise(8)
			Wait

		Fire:
			AK47 A 0 A_JumpIfInventory("AKFillAmmo",1,2)
			Goto NoAmmo
			TNT1 AAAA 0
			AK47 A 0 A_AlertMonsters
			AK47 A 0 A_ZoomFactor(0.96)
			AK47 B 1 BRIGHT A_FireBullets (1, 1, -1, 50, "BPUFF", FBF_NORANDOM)
			AK47 B 0 A_FireCustomMissile("AKTRACER")
			AK47 B 0 A_PlaySoundEx("akfr","SoundSlot7")
			AK47 B 0 A_ZoomFactor(1.0)
			AK47 B 0 A_Takeinventory("AKFillAmmo",1)
			AK47 B 1 A_GUNFLASH 
			AK47 B 1
			AK47 C 1
			AK47 A 2
			AK47 C 0 A_FireCustomMissile("AKrifleCaseSpawn",6,0,0,-5)
			AK47 A 0 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
			AK47 A 0 a_REFIRE
			Goto Ready2
			
		NoAmmo: 
			AK47 A 1
		NOAMMO2:	
			tnt1 a 0 A_PlaySound("AkEmptyclick")
			AK47 A 5 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOFIRE)
			AK47 A 0 A_JumpIfInventory("clipak",1,"Reload")
			GOTO READY2
		
		
		Reload:
			AK47 A 1 A_WeaponReady
			AK47 A 0 A_JumpIfInventory("AKFillAmmo",20,"ready" )
			AK47 A 0 A_JumpIfInventory("clipak",1,2)
			Goto NoAmmo
			TNT1 AAAA 0
			AK47 A 0 A_ZoomFactor(1.0)
			TNT1 AAAA 0
			NULL A 0 A_PlaySound("AKreload",1)
			AK47 DEFG 1
			AK47 HIJK 2
			NULL A 0 A_PlaySound("AKCLIP",2)
			AK47 LMNNN 2
			AK47 NNMLK 2
			AK47 JIHGF 2
			NULL A 0 A_PlaySound("AKRELOAD",3)
			AK47 EE 2
			AK47 Q 2
			AK47 RRRS 2
			TNT1 AAAAA 0
			
		InsertBullets:
			TNT1 AAAA 0
			AK47 A 0 A_JumpIfInventory("AKFillAmmo",20,10)
			AK47 A 0 A_JumpIfInventory("clipak",1,3)
			Goto Ready2
			TNT1 AAAAAA 0
			AK47 A 0 A_Giveinventory("AKFillAmmo",1)
			AK47 A 0 A_Takeinventory("clipak",1)
			Goto InsertBullets
			TNT1 AAAAAA 0
			AK47 A 0 A_Refire
			Goto Ready2
			TNT1 AAAA 0
			AK47 A 0 A_Refire
			Goto Ready2
			
		Flash:
			AK4F A 0 A_Light2
			AK4F A 2 A_Light1
			AK4F A 0 A_Light0 
			Stop
		
		Spawn:
			AVKL A -1
			Stop
	}
}

// Rocketz
Actor RocketzLauncher : weapon 29720
{
	//$Category Weapons
	//$Title Template Rocket Launcher
  Weapon.SelectionOrder 5500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "TemplateRocketz"
  Weapon.SlotNumber 5
  attacksound ""
  +WEAPON.NOAUTOAIM
  Inventory.PickupMessage "$GOTLAUNCHER"
  States
  {
  Ready:
    LNCH A 1 A_WeaponReady
    Loop
  Deselect:
    LNCH A 1 A_Lower
    Loop
  Select:
    LNCH AAAAAAAAAAAAAAAAAAAAAAA 1 A_Raise(4)	
    LNCH BCDE 1 a_WEAPONREADY
	LNCR RQPONM 1 a_WEAPONREADY
	LNCR MMMM 2 a_WEAPONREADY
	LNCH FGHIJKCB 2 a_WEAPONREADY
	LOOP
  Fire:
    LNCH A 0 A_zoomfactor(0.96) 
    LNCH A 1 A_firecustommissile("FillRocketz",0,1)
	TNT1 A 0 A_GunFlash
	LNCH O 3 A_recoil(3)
	LNCH P 5 A_zoomfactor(1.0)
	LNCH O 0 A_CheckforReload(1,"Ready")
	
	Goto Reload+1 
	
  Reload:
    LNCH D 0 A_CheckforReload(1,"Ready",true)
    LNCH DEEEEEEFG 1 
	LNCH H 2 
	TNT1 A 0 A_playsound("RktzRld")
	LNCH IJ 2
	LNCH JKCB 2
	goto ready
    	
	
  Flash:
    TNT1 A 3 Bright A_Light1
    LNCH N 3 Bright
    TNT1 A 3 Bright A_Light2
    Goto LightDone
  Spawn:
    LNCH Z -1
    Stop
  }
}

// 1111111111111111 AMMO 1111111111111111111

Actor TemplateRocketz : Ammo 29509
{
	//$Category Ammunition
	//$Title Rocket Template
	Inventory.Amount 30
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 50
	Ammo.BackpackMaxAmount 400
	Inventory.PickupMessage "$PICKROCKET"
	Inventory.Icon "roc2a0"
	
	States
	{
	Spawn:
		//TESL O -1
		ROC2 A -1
		Stop
	}
}

Actor BoxRocketz : TemplateRocketz 29511
{
	//$Category Ammunition
	//$Title Box of Template Rocket
	Inventory.Amount 60
	Inventory.PickupMessage "$GOTROCKBOX"
	
	States
	{
		Spawn:
			BRC2 A -1
			Stop
	}
}

//ACTOR Clip2 : Ammo Replaces Clip 
ACTOR ClipColt : Ammo 29634
{
	//$Category Ammunition
	//$Title Colt Clip Template
	// or ACTOR EXAMPLE NAME : AMMO 15***
	Inventory.PickupMessage "$PICKAMMOAN"
	Inventory.Amount 10
	Inventory.MaxAmount 200
	Inventory.Icon "CLP2A0"
	Inventory.PickupSound ""
	
	Ammo.BackpackAmount 30
	Ammo.BackpackMaxAmount 400
	Scale 0.9
	
	States
	{
		Spawn:
			CLP2 A -1
			Stop
	}
}

Actor BoxColt : ClipColt 29645
{
	//$Category Ammunition
	//$Title Box of Template Colt Clip
	Inventory.Amount 50
	Inventory.PickupMessage "$PICKAMMOAN"
	States
	{
		Spawn:
			BCLT A -1
			Stop
	}
}

//ACTOR ClipAK : Ammo Replaces Clip // for testing purposes.replace with name and editor number.
ACTOR ClipAK : Ammo 29635
{
	//$Category Ammunition
	//$Title AK-47 Clip Template
	Inventory.PickupMessage "$PICKAMMOAK"
	Inventory.Amount 20
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 200
	Ammo.BackpackMaxAmount 400
	Inventory.Icon "CLP3A0"
	Inventory.PickupSound "CLIPICK2"
	Scale 0.9
	States
	{
	Spawn:
		CLP3 A -1
		Stop
	}
}

Actor BoxAK : ClipAK 29636
{
	//$Category Ammunition
	//$Title Box of Template AK-47 Clip
	Inventory.Amount 60
	Inventory.PickupMessage "$PICKAMMOAK"
	States
	{
		Spawn:
			BAVK A -1
			Stop
	}
}

// aaaaaaaaaaaa FILL AMMO aaaaaaaaaaaaaaa

Actor AKFillAmmo : ammo 
 {
	Inventory.Amount 0
	Inventory.MaxAmount 20
	Inventory.PickupMessage "$PICKAMMOAK"
	Inventory.Icon clp3a0 
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 20
	
	States
	{
	Spawn:
		CLP3 A -1
		Stop
	}
}

ACTOR ColtFillAmmo : Ammo 
{
   Inventory.Amount 0
   Inventory.MaxAmount 9
   Inventory.Icon Clp2a0 // Important. Its the icon that appears on ZDoom fullscreen hud to represent the icon
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 9
}

ACTOR FillRocketz
{
	PROJECTILE
	-SKYEXPLODE
	+FORCEPAIN
	+EXPLODEONWATER
	+EXTREMEDEATH
	+NOGRAVITY
	DAMAGETYPE Explosive
	Radius 4
	Height 4
	Speed 20
	Damage 20
	Obituary "$ROCKETZDEATH"
	Scale -0.5
	
	
	
	States
	{
	Spawn:
	    tnt1 a 0
	    TNT1 A 0 A_playsound("rktzfire",2,0.3,false,ATTN_IDLE)
		MISL A -1 
		TNT1 A 0
		  
		
	Death: 
	    TNT1 A 0
		TNT1 A 0 a_playsound("rktzxpl",3,8,false,attn_norm)
	    MISL B 8 Bright A_Explode
		MISL C 6 Bright a_stopsound(2)
		MISL D 4 Bright 
		
		Stop
	}
}

// 

/*
---------------- SHELLS ----------------
*/
ACTOR ShellSpawn
{
	Speed 20
	
	PROJECTILE
	+NOCLIP
	+CLIENTSIDEONLY
	
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_CustomMissile("EmptyShell",1,0,random(-80,-100),2,random(45,80))
			Stop
	}
}

ACTOR PistolShellSpawn : ShellSpawn
{
	States
	{
		Spawn:
			TNT1 A 1 A_CustomMissile("PistolShell",0,0,random(-80,-100),2,random(40,60))
			Stop
	}
} 

ACTOR EmptyShell
{
   Height 2
   Radius 2
   Speed 6
   Scale 0.15
   Mass 0
   BounceFactor 0.3
   DeathSound "SHLCLT"
   BounceSound "SHLCLT"
   
   +MISSILE
   +DOOMBOUNCE
   +WINDTHRUST
   +CLIENTSIDEONLY
   +MOVEWITHSECTOR
   +NOBLOCKMAP
   +NOTELEPORT
   +FORCEXYBILLBOARD
   +NOTDMATCH
   +GHOST
   +FLOORCLIP
   +THRUACTORS
   -CANBOUNCEWATER
   -NOGRAVITY
   -DROPOFF
   
   States
   {
	   Spawn:
		  SHAL FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
		  SHAL FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
		  Stop

	   Death:
		  SHEL A 0 A_Jump(256,"Rest1","Rest2","Rest3","Rest4","Rest5","Rest6","Rest7","Rest8")
		Rest1:
		  SHAL I 600
		  SHAL I -1
		  stop
		Rest2:
		  SHAL J 600
		  SHAL J -1
		  stop
		Rest3:
		  SHAL K 600
		  SHAL K -1
		  stop
		Rest4:
		  SHAL L 600
		  SHAL L -1
		  stop
		Rest5:
		  SHAL M 600
		  SHAL M -1
		  stop
		Rest6:
		  SHAL I 600
		  SHAL I -1
		  stop
		Rest7:
		  SHAL J 600
		  SHEL J -1
		  stop
		Rest8:
		  SHAL K 600
		  SHAL K -1
		  stop
   }
}

ACTOR Pistolshell: EmptyShell
{
   Scale 0.1
   
   States
   {
	   Spawn:
		  SHAL FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
		  STOP

	   Death:
		  SHAL A 0 A_Jump(256,"Rest1","Rest2","Rest3","Rest4","Rest5")
		Rest1:
		  SHAL I 600
		  stop
		Rest2:
		  SHAL J 600
		  stop
		Rest3:
		  SHAL K 600
		  stop
		Rest4:
		  SHAL L 600
		  stop
		Rest5:
		  SHAL M 600
		  stop

		
		Stoping:
		  TNT1 A 0
		  Stop
   }
}

ACTOR AKRifleCaseSpawn
{
	Speed 20
	PROJECTILE
	+NOCLIP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
        TNT1 A 0
		TNT1 A 1 A_CustomMissile("EmptyBrass",1,0,random(-80,-100),2,random(40,70))
		Stop
	}
}
ACTOR EmptyBrass
{
   Height 2
   Radius 2
   Speed 5
   Scale 0.15
   +DOOMBOUNCE
   -CANBOUNCEWATER
   - NOGRAVITY
   +WINDTHRUST
   +CLIENTSIDEONLY
   +MOVEWITHSECTOR
   +MISSILE
   +NOBLOCKMAP
   -DROPOFF
   +NOTELEPORT
   +FORCEXYBILLBOARD
   +NOTDMATCH
   +GHOST
   //+DONTSPLASH
   //+CANBOUNCEWATER
   +FLOORCLIP
   +THRUACTORS
   Mass 0
   BounceFactor 0.3
   DeathSound "weapons/akcasing"
   BounceSound "weapons/akcasing"
   States
   {
   Spawn:
      //C4S3 FGHABCDEFGHABCDEFGH 3 
	  TNT1 A 0
	  TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
	  C4S3 FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
	  C4S3 FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
      STOP

   Death:
      C4S3 I 0
      LCPJ A 0 A_Jump(256,"Rest1","Rest2","Rest3","Rest4","Rest5","Rest6","Rest7","Rest8")
      Goto Rest1
    Rest1:
      C4S3 I 600
	  C4S3 I -1
      stop
    Rest2:
      C4S3 J 600
	  C4S3 J -1
      stop
    Rest3:
      C4S3 K 600
	  C4S3 K -1
      stop
    Rest4:
      C4S3 L 600
	  C4S3 L -1
      stop
    Rest5:
      C4S3 M 600
	  C4S3 M -1
      stop
    Rest6:
      C4S3 I 600
	  C4S3 I -1
      stop
    Rest7:
      C4S3 J 600
	  C4S3 J -1
      stop
    Rest8:
      C4S3 K 600
	  C4S3 K -1
      stop
	 

   }
}

ACTOR Bchip
{
	Damage 10
	Decal BulletChip1
	+NOEXTREMEDEATH
	+BLOODSPLATTER
	+NODAMAGETHRUST
	+nogravity

	States
	{
		Spawn:
			PUFF A 0 A_SetScale(0.8,0.8) 

		Death: 
			PUFF D 1  Bright
			Stop
	}
}

ACTOR Bpuff
{
  Damage 10
  Decal BulletChip1
  +NOEXTREMEDEATH
  +BLOODSPLATTER
  +NODAMAGETHRUST
  +nogravity
  
  States
  {
  Spawn:
    PUFF A 0 A_setscale(0.8,0.8) 
	PUFF A 0 Bright
	
  Death: 
	PUFF D 1  Bright
	
    Stop
  }
}

Actor AKTracer 
{
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+DONTSPLASH
    +BLOODSPLATTER 
	+NOEXTREMEDEATH
	radius 2
	height 2
	speed 260
	renderstyle ADD
	alpha 0.9
	scale .30
		states
			{
		Spawn:
			PUFF A 1 BRIGHT
			Loop
		Death:
			TNT1 A 0
			TNT1 A 1
			Stop
			
		XDeath:
			TNT1 A 0
			Stop
			}
}