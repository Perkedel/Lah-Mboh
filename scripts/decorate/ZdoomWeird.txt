// Other Weird ZDoom Wiki stuffs
// Switching Decorations
// https://zdoom.org/wiki/Creating_decorations_that_can_be_(de)activated
// Torch switching, was 10242

Actor SwitchableTorch : SwitchingDecoration 28235
{
	//$Category "Light sources"
	//$Title "Switchable Torch"
  Height 40
  Radius 20
  Activation THINGSPEC_Activate
  +SOLID
  +USESPECIAL 
  States
  {
  Spawn:
    //TRCH A 10
    CAND A 10
    Loop
  Active:
    //TRCH B 10 A_PlaySound("torch/start")
    //TRCH B 10
    CAND A 10 A_PlaySound("vile/firestrt") 
    CAND A 10 Bright
    Wait
  }
}

// Configurable Decorate
// https://zdoom.org/wiki/Making_configurable_actors_in_DECORATE
// Elite Cacodemon, was 8374
Actor EliteCaco : Cacodemon 27642
{
	//$Title Elite Cacodemon
	//$Category Monsters
	//$Arg0 Attack modes
	//$Arg0Tooltip "Toggles between the Cacodemons' different AI modes:\nStandard: Normal Cacodemon behaviour\nEvasive: Attempts to avoid being hit by its' target\nEvasive & Aggresive: Avoids being hit and attacks more fiercely"
	//$Arg0Type 11
	//$Arg0Enum {0 = "Standard"; 1 = "Evasive"; 2 = "Evasive & Aggressive";}
	//$Arg1 Field of view
	//$Arg1Tooltip "The Cacodemons' field of view."
	Health 600
	Speed 12
	Translation "170:191=209:223", "16:47=209:223", "164:167=220:223"
	Obituary "$ELITECACODTH"
	HitObituary "$ELITECACOATE"
	Var Int User_FOV;
	States
	{
		Spawn:
			TNT1 A 0
			
			//Field of view changing code.
			TNT1 A 0 A_JumpIf (args[1] == 0,3) //If the FOV argument is 0, skip the next 2 state frames.
			TNT1 A 0 A_SetUserVar (User_FOV,args[1]) //Set the FOV variable to whatever non-0 value was given the toe FOV argument.
			TNT1 A 0 A_Jump (256,2) //Skip the default FOV setting.
			TNT1 A 0 A_SetUserVar (User_FOV,180) //Standard monster FOV.
			
			//If the attack mode argument isn't 0, go to the alternate Spawn/look state.
			TNT1 A 0 A_JumpIf(args[0] == 1,"LookNervous")
			TNT1 A 0 A_JumpIf(args[0] == 2,"ChangeBehaviour") //If set to aggresive, change its' behaviour first.
			
			HEAD A 6 A_LookEx (0,0,0,0,User_FOV)
			Wait //Loop last frame.
		ChangeBehaviour: //Change the Cacodemons' flags and properties, then fall through to the LookNervous state.
			TNT1 A 0 A_ChangeFlag ("MissileEvenMore",True)
			TNT1 A 0 ACS_NamedExecute ("TriggerAggresiveBehaviour")
						
		LookNervous: //The cacodemon actively looks and moves around.
			HEAD A 16 A_SetTics (Random(16,70)) //Random delay.
			HEAD A 6 A_SetAngle (Angle+Random(45,-45))
			HEAD A 3 A_LookEx (0,0,0,0,User_FOV)
			HEAD AAAA 6 A_Wander
			Loop
		SeeEvasive: //The See state, but with more swerving around.
			HEAD A 2 A_Chase ("Missile","Missile",CHF_FASTCHASE)
			Loop
		See:
			HEAD A 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIf(args[0] == 1,"MissileEvade")
			TNT1 A 0 A_JumpIf(args[0] == 2,"AngryMissileEvade")
			HEAD BC 4 A_FaceTarget
			HEAD D 3 Bright A_HeadAttack
			Goto See
		MissileEvade: //The Cacodemon attacks normally, but also attempts to avoid being hit.
			HEAD BBBBB 1 A_FaceTarget (10)
			HEAD C 5 A_FaceTarget
			HEAD C 0 A_ChangeVelocity (CallACS("ACS_RandomPick",16,-16),0,0,CVF_RELATIVE)
			HEAD D 5 Bright A_HeadAttack
			HEAD CB 3 A_Recoil (6) //Move away from the target.
			Goto SeeEvasive
		AngryMissileEvade: //Ditto, but fires multiple fireballs now.
			HEAD BBBBB 1 A_FaceTarget (10)
			HEAD C 5 A_FaceTarget
			HEAD C 0 A_ChangeVelocity (CallACS("ACS_RandomPick",16,-16),0,0,CVF_RELATIVE)
			HEAD D 5 Bright A_CustomMissile ("CacodemonBall",32,0,0,CMF_CHECKTARGETDEAD)
			HEAD D 0 A_CustomMissile ("CacodemonBall",32,0,-16,CMF_CHECKTARGETDEAD)
			HEAD D 0 A_CustomMissile ("CacodemonBall",32,0,16,CMF_CHECKTARGETDEAD)
			HEAD D 0 A_FaceTarget
			HEAD CB 3 A_Recoil (6) //Move away from the target.
			Goto SeeEvasive
	}
}

// User Variables
// https://zdoom.org/wiki/User_variable
//ACTOR SpecialZombieMan : ZombieMan 27555
ACTOR RocketZombieMan : CustomizableZombieman
{
    var int user_rockets;
	//$Title Former Human (Rocket)
	//$Category Monsters

    States
    {
		Spawn:
			POSS A 0 NoDelay
			{ // See anonymous functions.
				user_rockets = 5;
			}

		Idle:
			Goto Super::Spawn

		Missile:
			POSS E 10 A_FaceTarget
			POSS F 8
			{
				If(user_rockets > 0)
				{
					A_CustomMissile("Rocket");
					user_rockets--;
				}
				Else
				{
					A_PosAttack;
				}
			}
			POSS E 8
			Goto See
    }
}

// Global variables
// https://zdoom.org/wiki/ZScript_global_variables
// Pointer
// https://zdoom.org/wiki/Actor_pointer#ZScript
// https://zdoom.org/wiki/Actor_states#Flow_control flow control
// https://zdoom.org/wiki/A_FadeTo Fade
// https://zdoom.org/wiki/A_FadeOut Fade once
// Fade Cacodemon projectile
ACTOR Fadeball : CacodemonBall
{
  RenderStyle "Translucent"
  Alpha 1.0
  States
  {
  Spawn:
    BAL2 AB 4 Bright A_FadeOut(0.2)
    Loop
  }
}

// https://zdoom.org/wiki/A_FadeIn fadein
ACTOR Ghostball : CacodemonBall
{
  RenderStyle "Translucent"
  Alpha 0
  States
  {
  Spawn:
    BAL2 AB 4 Bright A_FadeIn(0.2)
    Loop
  }
}

// Cool Plasma Ball
//Actor CoolPlasmaBall : PlasmaBall replaces PlasmaBall
Actor CoolPlasmaBall : PlasmaBall
{
   Radius 9
   Height 6
   Scale 0.75
   RenderStyle Add
   Alpha 0.8
   
   States
   {
   Spawn:
     PLSS AAAAAABBBBBB 1 Bright A_SpawnItemEx("CPBTrail", 0, 0, 0, 0, 0, 0, 0, 0)
     Loop
   }
}

// & Trail
Actor CPBTrail
{
   Radius 9
   Height 6
   Scale 0.75
   RenderStyle Add
   Alpha 0.8
   
   +NOGRAVITY
   +NOBLOCKMAP
   +NOCLIP
   
   States
   {
   Spawn:
     PLSS AAAAAABBBBBB 1 Bright A_FadeOut
     Loop
   }
}

