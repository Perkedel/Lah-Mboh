//Zombie Missile Tank
/*
Name: Zombie Missile Tank
Difficulty: Medium
Connection: ZombieTank
Summon: ZombieMissileTank
Melee: No
Distance: Projectile
Type: Human, Undead, Mechanic
Brightmaps: Yes
Added States: none
ACS: none

Decorate: Xim
GLDEFs: Dreadopp, Ghastly Dragon, Xim (Brightmaps)
Sounds: Id Software, 3DRealms
Sprites: Id Software, 3DRealms, David G
Sprite Edit: Xim
Idea Base: Pig Tank from Duke Nukem 3D
*/

// was 21014
ACTOR ZombieMissileTank 21714
{
	Health 575
	Painchance 40
	Speed 5
	Radius 27
	Height 72
	Mass 1100
	SeeSound "ZMTank/see"
	AttackSound "ZMTank/missile"
	PainSound "ZMTank/pain"
	DeathSound "ZMTank/death"
	Obituary "%o was exploded by a zombie in missile a tank."
	MONSTER 
	+FLOORCLIP
	+NORADIUSDMG
	+NOBLOOD
	+MISSILEMORE
	States
	{
		// TODO: Implement Press the butt to explode like original Duke Nukem 3D
	Spawn:
		ZMTK A 8 A_Look
		Loop
	See:
		TNT1 A 0 A_PlaySound ("ZMTank/Run")
		ZMTK AABBCC 1 A_Chase
		Loop
	Missile:
		ZMTK A 4 A_FaceTarget
		ZMTK D 4 Bright A_CustomMissile("ZombieTankMissile",55,32,359,0) 
		ZMTK A 4 A_FaceTarget
		ZMTK E 4 Bright A_CustomMissile("ZombieTankMissile",55,-29,359,0) 
		ZMTK A 4
		Goto See
	Pain:
		ZMTK F 4 A_Pain
		ZMTK F 4
		Goto See
	Death:
		ZMTK G 4
		ZMTK G 4 A_Scream
		ZMTK H 8 A_SpawnItemEx("ZombieMissileTankGun", -40, 0, 30, 12, 0, 0, 64, 128)
		ZMTK I 8 A_SpawnItemEx("ZombieMissileTankGun", 40, 0, 30, 12, 0, 0, 192, 128)
		ZMTK J 8 A_CustomMissile ("ZombieTankExplosion", 48, 0, 0)
		TNT1 A 0 A_SpawnItemEx("ShotgunGuy",0,0,20,0,0,3,0,SXF_TRANSFERPOINTERS)
		ZMTK K 8 A_Fall
		ZMTK L 8
		ZMTK M 8 A_SpawnItemEx("ZTankSmoke", 0, 0, 48, 0, 0, 0, 0, 128)
		ZMTK N 8
		ZMTK O -1
		Stop
	}
}

ACTOR ZombieTankMissile
{
	Radius 8
	Height 11
	Speed 25
	Damage 10
	PROJECTILE
	DeathSound "ZMTank/missileX"
	ExplosionDamage 54
	ExplosionRadius 76
	+Rockettrail
	+THRUGHOST
	+SEEKERMISSILE
	SeeSound "ZMTank/missile"
	Decal Scorch
	States
	{
	Spawn:
		ZMIS A 2 Bright A_Tracer
		ZMIS A 5 Bright
		Goto Spawn
	Death:
		ZMIS C 0 Bright A_SetTranslucent (0.67,1)
		ZMIS B 8 Bright A_Explode
		ZMIS CD 5 Bright
		stop
	}
}

ACTOR ZombieMissileTankGun
{
	Radius 4
	Height 4
	Mass 5
	Speed 12
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	+DOOMBOUNCE
	+NoTarget
	States
	{
	Spawn:
		ZTNM A 3
		ZTNM B 3
		ZTNM C 3
		ZTNM D 3
		ZTNM E 3
		ZTNM F 3
		ZTNM G 3
		Loop
	Death:
		ZTNM G 1
		ZTNM G -1
		Stop
	}
}