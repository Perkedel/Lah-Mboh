// Hey would you look at that
// https://zdoom.org/wiki/Projectile_Trap
// https://zdoom.org/wiki/Editor_keys
// https://zdoom.org/wiki/Making_configurable_actors_in_DECORATE
// A projectile launcher!
// Was 20005

ACTOR TrapProjectileLauncher 28225
{ 
	//$Category "Monsters"
	//$Title "Trap Projectile Launcher"
	//$Arg0 "Mode"
	//$Arg1 "Type"
	//$Arg2 "Speed"
	//$Arg3 "Pitch"
	//$Arg4 "FOV"
	//
	//$Arg0Enum {1="Constant"; 2="Automatic"; 3="Smart";}
	
	
	Monster
	+STANDSTILL
	+NOBLOCKMAP
	+NOGRAVITY
	-SHOOTABLE
	States
	{
	Spawn:
		AMRK A 0
		TNT1 A 5 A_LookEx (LOF_NOSOUNDCHECK, 0, 0, 0, 0, "SpawnSkip")
		Loop
	SpawnSkip:
	 	TNT1 A 5
		TNT1 A 0 A_JumpIf (args[0]==1, "Constant")
		TNT1 A 0 A_JumpIf (args[0]==2, "Automatic")
		TNT1 A 0 A_JumpIf (args[0]==3, "Smart")
       
	Constant: //First Function: Always Shoot
		TNT1 A 0 Thing_Projectile (0, args[1], (angle * 256 / 360)%256, args[2], args[3])
		TNT1 A 0
		Goto Spawn
       
	Automatic: //Second Function: Shoot when in sight
		TNT1 A 0 A_JumpIfTargetInLOS (2,args[4]*256)
		TNT1 A 0 A_Jump (256,2)
		TNT1 A 0 Thing_Projectile (0, args[1], (angle * 256 / 360)%256, args[2], args[3])
		TNT1 A 0
		Goto Spawn
	   
	Smart: //Third Function: Same as second, but can turn.
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_JumpIfTargetInLOS (2,args[4]*256)
		TNT1 A 0 A_Jump (256,2)
		TNT1 A 0 Thing_Projectile (0, args[1], (angle * 256 / 360)%256, args[2], args[3])
		TNT1 A 0
		Goto Spawn
	}
}